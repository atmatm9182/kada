#!/usr/bin/env sh
# -*- mode: scheme; -*-
# vim: ft=scheme
exec guile --no-auto-compile -e main -s "$0" "$@"
!#

(use-modules (ice-9 match)
             (ice-9 format)
             (srfi srfi-9)
             (kada db)
             (kada types))
;; (print
;;  (strftime "%c"
;;            (localtime
;;             (current-time))))

(define main
  (match-lambda
    ((program cmd rest ...)
     (match (assoc cmd *cmd-handlers*)
       (#f (error (format #f "Uncrecognized subcommand '~a'" cmd)))
       ((_ . handler) (handler rest))))
    ((program rest ...)
     (error "No subcommand provided. Aborting."))))

(define (item-enter args)
  (when (not (= (length args) 1))
    (error "Invalid number of arguments for 'enter'"))
  (let* ((name (car args))
         (mark (make-mark name (current-time) #f #t)))
    (db-insert-mark! mark)
    (format #t "Successfully created mark '~a'\n" name)))

(define (item-leave args)
  (let ((last-mark (db-query-last-mark)))
    (when (eq? last-mark #f)
      (error "There are no marks"))

    (let ((name (mark-name last-mark)))
      (if (mark-enter? last-mark)
        (begin
          (db-insert-mark! (make-mark name
                                     (current-time)
                                     #f
                                     #f))
          (format #t "Successfully created mark '~a'\n" name))
        (error "Last mark with name '~a' is not an entry mark" name)))))

(define *cmd-handlers*
  `(("enter" . ,item-enter)
    ("leave" . ,item-leave)))
